pigquery=%default cf 'tasks_<<sys:lom>>'
%default fstart_time <<start:1>>L
%default fend_time <<end:1000000000000000>>L
%default ftp 'tstart'
%default resdir '/does/not/exist'

tsr = LOAD 'cassandra://hopsa/$cf' USING CassandraStorage() AS (k:bytearray, va:{t:(
		n:bytearray, v:bytearray)});
ts1 = FOREACH tsr {
	 u = FILTER va BY n=='user';
	 nc = FILTER va BY n=='ncpus';
	 tf = FILTER va BY n=='$ftp';
	 GENERATE k AS key, FLATTEN(u.v) AS user, FLATTEN(nc.v) AS ncpus,
		 FLATTEN(tf.v) AS tfilter;
};
ts = FOREACH ts1 GENERATE key, user, (long)ncpus, 1000L * (long)tfilter AS tfilter;
tsf = FILTER ts BY tfilter >= $fstart_time AND tfilter < $fend_time;
g1 = GROUP tsf BY user;
r1 = FOREACH g1 GENERATE group AS user, AVG(tsf.ncpus) AS ncpus;
tsa = FOREACH r1 GENERATE user,
	(ncpus <= 8 ? '1-8' :
	(ncpus <= 16 ? '9-16' :
	(ncpus <= 32 ? '17-32' :
	(ncpus <= 64 ? '33-64' :
	(ncpus <= 128 ? '65-128' :
	(ncpus <= 512 ? '129-512' : '513-65536')))))) AS cpuclass;
g = GROUP tsa BY cpuclass;
res = FOREACH g GENERATE group AS cpuclass, COUNT(tsa.user) AS nusers;
STORE res INTO '$resdir' USING PigStorage(',');

columns=cpuclass, nusers_num

